apply plugin: 'groovy'
apply plugin: 'fatjar'
apply plugin: 'application'
apply plugin: 'project-report'

// The main class of the application
mainClassName = 'com.example.helloworld.HelloWorldService'
dropWizardConfig = './src/dist/config/example.yml'

// Use Java 7 by default
sourceCompatibility = 1.7
targetCompatibility = 1.7

// Add Gradle FatJar Plugin, see https://github.com/musketyr/gradle-fatjar-plugin
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath group: 'eu.appsatori', name: 'gradle-fatjar-plugin', version: '0.1.3'
    }
}

// Set our project variables
project.ext {
    dropwizardVersion = '0.6.1'
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.yammer.dropwizard', name: 'dropwizard-core', version: dropwizardVersion
    compile group: 'com.yammer.dropwizard', name: 'dropwizard-hibernate', version: dropwizardVersion
    compile group: 'com.yammer.dropwizard', name: 'dropwizard-migrations', version: dropwizardVersion
    compile group: 'com.yammer.dropwizard', name: 'dropwizard-auth', version: dropwizardVersion
    compile group: 'com.h2database', name: 'h2', version: '1.3.170'
    testCompile group: 'com.yammer.dropwizard', name: 'dropwizard-testing', version: dropwizardVersion

    // this dependency is needed due to http://issues.gradle.org/browse/GRADLE-2647
    compile "com.sun.jersey:jersey-client:1.15" 
    groovy group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.1.0'
    
    // spock
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
    testCompile "org.hamcrest:hamcrest-core:1.3" // only necessary if Hamcrest matchers are used      
    testRuntime "cglib:cglib-nodep:2.2.2"     // enables mocking of classes (in addition to interfaces)
    testRuntime "org.objenesis:objenesis:1.2" // enables mocking of classes without default constructor (together with CGLIB)

}

// Configure the fatJar task
fatJar {
    classifier 'fat'

    // We need to add the main class to the manifest, otherwise the JAR won't start.
    manifest {
        attributes 'Main-Class': mainClassName
    }

    // We have to exclude signature files from the fat JAR, otherwise we'll get invalid signature file digest errors.
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
}

// Configure the run task to start the Dropwizard service

run {
    args 'server', dropWizardConfig
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.4'
}
